Typer {
    scope[a](body : () => a) : a
    topScope[a](body : () => a) : a
    type(name : String) : TypeDefinition
    variable(name : String) : Type
    function(name : String) : Option[FunctionDefinition]
    bindFunction(name : String, definition : FunctionDefinition)
    bindVariable(name : String, type : Type)
    bindTypeVariable(position : Int, id : Int, type : Type)
    occursCheck(position : Int, id : Int, outerType : Type, type : Type)
    fresh() : Int
    expand(type : Type) : Type
    instantiate(instantiation : StringMap[Type], type : Type) : Type
    setModule(newModule : Module)
    error[a](position : Int, problem : String) : a
}

newTyper(modules : Array[Module]) : Typer {
    types := newStringMapBuilder(preludeTypeDefinitions().concat(flatten(map(modules, m => map(m.typeDefinitions, d => Pair(d.symbol, d))))))
    functions := newStringMapBuilder(flatten(map(modules, m => map(m.functionDefinitions, d => Pair(d.signature.symbol, d)))))
    variables := newStringMapBuilder([])
    substitution := newStringMapBuilder([])
    nextId := 0
    module := modules(0)
    Typer { typer =>
        scope(body) {
            oldVariables := variables.toArray
            oldFunctions := functions.toArray
            result := body()
            variables = newStringMapBuilder(oldVariables)
            functions = newStringMapBuilder(oldFunctions)
            result
        }
        topScope(body) {
            result := typer.scope(body)
            substitution = newStringMapBuilder([])
            nextId = 0
            result
        }
        type(name) {
            types(name)
        }
        function(name) {
            functions.get(name)
        }
        variable(name) {
            variables(name)
        }
        bindVariable(name, type) {
            variables.set(name, type)
        }
        bindFunction(name, definition) {
            functions.set(name, definition)
        }
        bindTypeVariable(position, id, type) {
            typer.occursCheck(position, id, type, type)
            when(substitution.has(id.toString), { typer.error(position, "Type variable already bound: _\(id.toString)") })
            substitution.set(id.toString, type)
        }
        occursCheck(position, id, outerType, type) {
            typer.expand(type) ? {
                variable(p2, id2) { when(id == id2, { js"debugger;"; typer.error(position, "Infinite type: _\(id.toString) = \(typeToString(typer.expand(outerType)))") }) }
                constructor(p2, symbol2, typeArguments2) { each(typeArguments2, a => typer.occursCheck(position, id, outerType, a)) }
                record(p2, fields2) { each(fields2, f => typer.occursCheck(position, id, outerType, f.type)) }
                parameter(p2, name2) {}
            }
        }
        fresh() {
            nextId += 1
            nextId
        }
        expand(type) {
            type ? {
                variable(p2, id2) { substitution.get(id2.toString) ? { some(t) { typer.expand(t) }; none { type } } }
                constructor(p2, symbol2, typeArguments2) { Type.constructor(p2, symbol2, map(typeArguments2, a => typer.expand(a))) }
                record(p2, fields2) { Type.record(p2, map(fields2, f => FieldType(f.position, f.label, typer.expand(f.type)))) }
                parameter(p2, name2) { type }
            }
        }
        instantiate(instantiation, type) {
            type ? {
                variable(position, id) { type }
                parameter(position, name) {
                    instantiation.get(name) ? {
                        some(t) { t }
                        none { type }
                    }
                }
                record(position, fields) {
                    Type.record(position, map(fields, f => FieldType(f.position, f.label, typer.instantiate(instantiation, f.type))))
                }
                constructor(position, symbol, typeArguments) {
                    Type.constructor(position, symbol, map(typeArguments, t => typer.instantiate(instantiation, t)))
                }
            }
        }
        setModule(newModule) {
            module = newModule
        }
        error(position, problem) {
            moduleName := module.file
            js"console.log('Type error in ' + moduleName + ':')"
            panic("\(problem) \(positionText(newCharCursor(module.source), position))")
        }
    }
}

equalityConstraint(typer : Typer, position : Int, expectedType : Type, actualType : Type) : Void {
    solveEqualityConstraint(typer, position, expectedType, actualType, expectedType, actualType)
}

solveEqualityConstraint(typer : Typer, position : Int, originalExpectedType : Type, originalActualType : Type, expectedType : Type, actualType : Type) : Void {
    error := {
        typer.error(position, "Expected \(typeToString(typer.expand(originalExpectedType))), got \(typeToString(typer.expand(originalActualType)))")
    }
    typer.expand(expectedType) ? {
        variable(p1, id1) {
            typer.expand(actualType) ? {
                variable(p2, id2) {
                    when(id1 != id2, {
                        typer.bindTypeVariable(position, id1, actualType)
                    })
                }
                record(p2, fields2) { typer.bindTypeVariable(position, id1, actualType) }
                constructor(p2, symbol2, typeArguments2) { typer.bindTypeVariable(position, id1, actualType) }
                parameter(p2, name2) { typer.bindTypeVariable(position, id1, actualType) }
            }
        }
        record(p1, fields1) {
            typer.expand(actualType) ? {
                variable(p2, id2) { typer.bindTypeVariable(position, id2, expectedType) }
                constructor(p2, symbol2, typeArguments2) { error() }
                record(p2, fields2) {
                    when(fields1.size != fields2.size, { error() })
                    each(zip(fields1, fields2), p => {
                        when(p.first.label != p.second.label, { error() })
                        solveEqualityConstraint(typer, position, originalExpectedType, originalActualType, p.first.type, p.second.type)
                    })
                }
                parameter(p2, name2) { error() }
            }
        }
        constructor(p1, symbol1, typeArguments1) {
            typer.expand(actualType) ? {
                variable(p2, id2) { typer.bindTypeVariable(position, id2, expectedType) }
                constructor(p2, symbol2, typeArguments2) {
                    when(symbol1 != symbol2, { error() })
                    when(typeArguments1.size != typeArguments2.size, { error() })
                    each(zip(typeArguments1, typeArguments2), p => solveEqualityConstraint(typer, position, originalExpectedType, originalActualType, p.first, p.second))
                }
                record(p2, fields2) { error() }
                parameter(p2, name2) { error() }
            }
        }
        parameter(p1, name1) {
            typer.expand(actualType) ? {
                variable(p2, id2) { typer.bindTypeVariable(position, id2, expectedType) }
                constructor(p2, symbol2, typeArguments2) { error() }
                record(p2, fields2) { error() }
                parameter(p2, name2) {
                    when(name1 != name2, { error() })
                }
            }
        }
    }
}

typeToString(type : Type) : String {
    type ? {
        variable(p2, id2) {
            "_\(id2.toString)"
        }
        constructor(p2, symbol2, typeArguments2) {
            as := map(typeArguments2, a => typeToString(a))
            joined := join(map(typeArguments2, t => typeToString(t)), ", ")
            name := symbol2
            js"name = name.replace(/[@].*/, '');"
            if(typeArguments2.size == 0, { "\(name)" }, { "\(name)[\(joined)]" })
        }
        record(p2, fields2) {
            joined := join(map(fields2, f => "\(f.label) : \(typeToString(f.type))"), ", ")
            "[\(joined)]"
        }
        parameter(p2, name2) {
            "\(name2)"
        }
    }
}


preludeTypeDefinitions() : Array[Pair[String, TypeDefinition]] {
    void := Type.constructor(0, "Void@_", [])
    int := Type.constructor(0, "Int@_", [])
    bool := Type.constructor(0, "Bool@_", [])
    string := Type.constructor(0, "String@_", [])
    typeDefinitions := [
        TypeDefinition(0, "Void@_", [], Bool.true, [
            MethodSignature(0, "void", [], [], void)
        ]),
        TypeDefinition(0, "Pair@_", ["a", "b"], Bool.true, [
            MethodSignature(0, "none", [], [], void),
            MethodSignature(0, "some", [], [Parameter(0, "first", Type.parameter(0, "a")), Parameter(0, "second", Type.parameter(0, "b"))], void)
        ]),
        TypeDefinition(0, "Option@_", ["a"], Bool.true, [
            MethodSignature(0, "none", [], [], void),
            MethodSignature(0, "some", [], [Parameter(0, "value", Type.parameter(0, "a"))], void)
        ]),
        TypeDefinition(0, "String@_", [], Bool.false, [
            MethodSignature(0, "size", [], [], int),
            MethodSignature(0, "take", [], [Parameter(0, "count", int)], string),
            MethodSignature(0, "drop", [], [Parameter(0, "count", int)], string),
            MethodSignature(0, "toLower", [], [], string),
            MethodSignature(0, "toUpper", [], [], string)
        ]),
        TypeDefinition(0, "Bool@_", [], Bool.true, [
            MethodSignature(0, "false", [], [], void),
            MethodSignature(0, "true", [], [], void)
        ]),
        TypeDefinition(0, "Int@_", [], Bool.false, [
            MethodSignature(0, "toString", [], [], string)
        ]),
        TypeDefinition(0, "Float@_", [], Bool.false, [
        ]),
        TypeDefinition(0, "Array@_", ["a"], Bool.false, [
            MethodSignature(0, "invoke", [], [Parameter(0, "index", int)], Type.parameter(0, "a")),
            MethodSignature(0, "size", [], [], int),
            MethodSignature(0, "take", [], [Parameter(0, "count", int)], Type.constructor(0, "Array@_", [Type.parameter(0, "a")])),
            MethodSignature(0, "drop", [], [Parameter(0, "count", int)], Type.constructor(0, "Array@_", [Type.parameter(0, "a")])),
            MethodSignature(0, "concat", [], [Parameter(0, "array", Type.constructor(0, "Array@_", [Type.parameter(0, "a")]))], Type.constructor(0, "Array@_", [Type.parameter(0, "a")]))
        ]),
        TypeDefinition(0, "F0@_", ["r"], Bool.false, [
            MethodSignature(0, "invoke", [], [], Type.parameter(0, "r"))
        ]),
        TypeDefinition(0, "F1@_", ["p1", "r"], Bool.false, [
            MethodSignature(0, "invoke", [], [Parameter(0, "a1", Type.parameter(0, "p1"))], Type.parameter(0, "r"))
        ]),
        TypeDefinition(0, "F2@_", ["p1", "p2", "r"], Bool.false, [
            MethodSignature(0, "invoke", [], [Parameter(0, "a1", Type.parameter(0, "p1")), Parameter(0, "a2", Type.parameter(0, "p2"))], Type.parameter(0, "r"))
        ])
    ]
    map(typeDefinitions, d => Pair(d.symbol, d))
}
